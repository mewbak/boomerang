dnl
dnl See the file "LICENSE.TERMS" for information on usage and
dnl redistribution of this file, and for a DISCLAIMER OF ALL
dnl WARRANTIES.
dnl
dnl File: configure.in
dnl autoconf uses this file to create the configure script, which generates
dnl the Makefiles and sets up various things.
dnl
dnl $Revision: 1.16 $
dnl
dnl 12 Apr 02 - Mike: Created
dnl 15 Oct 02 - Sebastian Apel: checks for libelf, libcppunit and AC_PREREQ inserted
dnl 06 Mar 03 - Mike: Removed need for libelf
dnl 27 Jun 04 - Mike: Added XMLWF for eXpat XML parser

AC_PREREQ(2.52)
AC_INIT(LICENSE.TERMS)
AC_CONFIG_HEADER(include/config.h)

AC_CANONICAL_SYSTEM


dnl Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PATH_PROG( BISONPP, bison++, BISONPP_NOT_INSTALLED )
AC_PATH_PROG( FLEXPP, flex++, FLEXPP_NOT_INSTALLED )
AC_SUBST( BISONPP )
AC_SUBST( FLEXPP )

AC_DEFUN(AC_PATH_BINUTIL, [
   for i in $2; do
      if test -f ${TARGET_PREFIX}$i; then
          AC_MSG_CHECKING([for ${TARGET_PREFIX}$i])
          $1=${TARGET_PREFIX}$i
	  AC_MSG_RESULT( [yes] )
      else
         AC_PATH_PROG( $1, ${TARGET_PREFIX}$i )
      fi
      if test -n "$$1"; then
         break
      fi
   done
   if test -z "$$1"; then
      # not found, try to fall back on basic versions
      AC_PATH_PROGS( $1, $2 )
   fi
] )

dnl -------------------
AC_PATH_BINUTIL( HOST_CC, gcc cc )

dnl -------------------
AC_MSG_CHECKING( [if using GNU ld] )
HOST_GNU_LD=no
if ${HOST_CC} -v 2>&1 </dev/null | egrep "gcc" 1>&5; then
   HOST_GCC=yes
   HOST_LD=`${HOST_CC} -print-prog-name=ld`
else
   HOST_LD=ld
fi
if ${HOST_LD} -v 2>&1 </dev/null | egrep '(GNU|with BFD)' 1>&5; then
   HOST_GNU_LD=yes
   AC_DEFINE( HOST_GNU_LD, 1, [Host uses GNU ld] )
fi
AC_MSG_RESULT( $HOST_GNU_LD )

dnl -------------------
AC_MSG_CHECKING( [if host is Windows] )
case $host in
 *-*-mingw32* | *-*-cygwin*)
   HOST_WINDOWS=yes
   ;;
 *)
   HOST_WINDOWS=no
   ;;
esac
AC_MSG_RESULT( $HOST_WINDOWS )

AC_MSG_CHECKING( [if host is Cygwin] )
case $host in
 *-*-cygwin*)
   HOST_CYGWIN=yes
   ;;
 *)
   HOST_CYGWIN=no
   ;;
esac
AC_MSG_RESULT( $HOST_CYGWIN )




dnl Checks for elf library (no longer needed)
dnl AC_CHECK_LIB(elf, elf_begin, , [AC_MSG_ERROR("library 'elf' not found")])

AC_CHECK_LIB(stdc++, main)

dnl Checks for cppunit library
AC_LANG_PUSH(C++)
CPPFLAGS="-Iinclude ${CPPFLAGS}"
AC_CHECK_HEADER(cppunit/TestSuite.h,,
  [AC_CHECK_FILE(include/cppunit/TestSuite.h,,
    [AC_MSG_WARN()]
    [AC_MSG_WARN(library 'cppunit' not installed... or you need to link)]
    [AC_MSG_WARN(include/cppunit to the cppunit include/cppunit directory.)]
    [AC_MSG_WARN()])])
AC_LANG_POP(C++)

dnl Check for eXpat includes
dnl Want to check default-includes PLUS the boomerang include/ directory
AC_CHECK_HEADER(expat.h,,
    [AC_CHECK_FILE(include/expat.h,,
        [AC_MSG_WARN()]
        [AC_MSG_WARN(Header file expat.h not found: eXpat not installed...)]
        [AC_MSG_WARN(or you need to copy expat.h to boomerang's include/ dir)]
        [AC_MSG_WARN()])])

dnl Check for eXpat library
AC_CHECK_LIB(expat, XML_ExpatVersion, ,
    [AC_CHECK_FILE(lib/libexpat.a, ,
        [AC_MSG_WARN()]
        [AC_MSG_WARN(eXpat library not found: expat not installed... or you)]
        [AC_MSG_WARN(need to copy libexpat.a (etc) to boomerang's lib/ dir)]
        [AC_MSG_WARN()]) ],)

dnl Checks for header files.
dnl AC_HEADER_STDC
dnl AC_CHECK_HEADERS(fcntl.h malloc.h sys/time.h unistd.h byteswap.h)
dnl AC_CHECK_HEADERS(libelf.h libelf/libelf.h, break)
dnl AC_CHECK_HEADERS(link.h sys/link.h, break)
dnl AC_CHECK_HEADERS(elf.h sys/elf_SPARC.h sys/auxv.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN

AC_CHECK_SIZEOF(char,1)
AC_CHECK_SIZEOF(short,2)
AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)
AC_CHECK_SIZEOF(long long,8)
AC_CHECK_SIZEOF(float,4)
AC_CHECK_SIZEOF(double,8)
dnl AC_CHECK_SIZEOF(long double,16)


dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL

dnl Now the options...
dnl AC_CHECK_FUNCS(mkdir rmdir)

dnl Actually we have "make remote" again now, so this is unused at present:
AC_ARG_ENABLE(remote, [  --enable-remote         don't try to regenerate source files],
              [ REMOTE=1 ], [ REMOTE=0 ] )
AC_SUBST(REMOTE)

dnl Three paths for frontend/mltk.sh
AC_PATH_PROG(TKML, .run-sml, /usr/share/sml-nj/bin/.run-sml,
    [$PATH /usr/share/sml-nj/bin $HOME/install/smlnj/bin])
dnl many more processors and platforms needed in this list
AC_PATH_PROGS(HEAPPATH,
    [sml-toolkit.x86-linux sml-toolkit.sparc-solaris],
    $HOME/install/sml-toolkit.x86-linux,
    [$HOME/install $PATH $HOME/install/mltk])
HEAP=\"@SMLload=$HEAPPATH\"
AC_SUBST(HEAP)


AC_PATH_PROG(UNGENERATE, ungenerate, $HOME/install/ungenerate,
    [$PATH:$HOME/install:$HOME/bin:/home/02/binary/u0.luna.tools/NJ/base])

PREFIX=$srcdir
LIBDIR=$srcdir/lib

AC_SUBST(PREFIX)
AC_SUBST(LIBDIR)
AC_SUBST(HOST_GNU_LD)
AC_SUBST(HOST_WINDOWS)
AC_SUBST(HOST_CYGWIN)


AC_CONFIG_FILES(          Makefile)
AC_CONFIG_FILES(     util/Makefile)
AC_CONFIG_FILES(   loader/Makefile)
AC_CONFIG_FILES(       db/Makefile)
AC_CONFIG_FILES( frontend/Makefile)
AC_CONFIG_FILES( analysis/Makefile)
AC_CONFIG_FILES(transform/Makefile)
AC_CONFIG_FILES(frontend/mltk.sh, [chmod +x frontend/mltk.sh])

AC_OUTPUT
